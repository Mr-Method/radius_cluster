#!perl

=head1 NAME

 Users tags

 Error ID: 21xx

=cut


use strict;
use warnings FATAL => 'all';

use Abills::Defs;
use Abills::Base qw(in_array);
use Tags;

our $db;
our $admin;
our %conf;
our $html;
our %lang;

my $Tags    = Tags->new($db, $admin, \%conf);

my @priority        = ($lang{VERY_LOW}, $lang{LOW}, $lang{NORMAL}, $lang{HIGH}, $lang{VERY_HIGH});
my @priority_colors = ('bg-muted', "bg-info", "bg-success", 'bg-warning', "bg-danger");

#*******************************************************************
=head2 iptv_user_del($uid, $attr) Delete user from module

=cut
#*******************************************************************
sub tags_user_del {
  my ($uid) = @_;

  $Tags->{UID} = $uid;
  $Tags->user_del({ UID => $uid });

  return 0;
}

#**********************************************************
=head2 tags_user($attr)

=cut
#**********************************************************
sub tags_user {
  my ($attr) = @_;

  if ($FORM{change}) {
    $Tags->tags_user_change(\%FORM);
    if (!_error_show($Tags, { ID => 2102 })) {
      $html->message('info', $lang{INFO}, "$lang{CHANGED}");
    }
  }
  elsif ($FORM{del} && $FORM{COMMENTS}) {
    $Tags->user_del({ ID => $FORM{del} });
    if (!_error_show($Tags, { ID => 2104 })) {
      $html->message('info', $lang{INFO}, "$lang{CHANGED}");
    }
  }

  my $list  = $Tags->tags_user({ %FORM, COLS_NAME => 1 });

  my $table = $html->table(
    {
      width      => '100%',
      caption    => "Tags",
      title      => [ '-', $lang{NAME}, $lang{DATE}, $lang{COMMENTS} ],
      FIELDS_IDS => $Tags->{COL_NAMES_ARR},
      cols_align => [ 'left', 'left', 'left', 'left' ],
      qs         => $pages_qs,
      ID         => 'TAG_USER',
    }
  );

  foreach my $line (@$list) {
    $table->addrow(
      #$line->{id}.
      $html->form_input(
        'IDS',
        $line->{id},
        {
          TYPE  => 'CHECKBOX',
          STATE => ($line->{date}) ? 1 : undef
        }
      ),
      $line->{name},
      $line->{date},
      $line->{comments}
    );
  }

  my $action = '';
  if ($attr && $attr->{ACTION}) {
    $action = $html->form_input('finish', $lang{REGISTRATION_COMPLETE}, { TYPE => 'submit' })
      . ' ' . $html->form_input('back', $lang{BACK}, { TYPE => 'submit' })
      . ' ' . $html->form_input('next', $lang{NEXT}, { TYPE => 'submit' });
  }
  else {
    $action = $html->form_input('change', "$lang{CHANGE}", { TYPE => 'submit' });
  }

  $table->{extra} = 'colspan=4';
  $table->addrow($action);

  my $service_info1 = $html->form_main(
    {
      CONTENT => $table->show({ OUTPUT2RETURN => 1 }),
      HIDDEN  => {
        index => $index,
        UID   => $FORM{UID},
        step  => $FORM{step}
      },
      METHOD   => 'GET',
      NAME     => $FORM{FORM_NAME} || 'TAGS_USER',
      ID       => $FORM{FORM_NAME} || 'TAGS_USER'
    }
  );

  if($attr->{PROFILE_MODE}) {
    return '', $service_info1;
  }

  print $service_info1;

  return 1;
}

#**********************************************************
=head2 tags_list($attr)

=cut
#**********************************************************
sub tags_list {
  $Tags->{ACTION}     = 'add';
  $Tags->{LNG_ACTION} = $lang{ADD};

  if( $FORM{add} ) {
    $Tags->add({ %FORM });

    if (! _error_show($Tags)) {
      $html->message('info', $lang{INFO}, "$lang{ADDED}");
      return 0;
    }
  }
  elsif( $FORM{change} ) {
    $Tags->change({ %FORM });

    if (! _error_show($Tags)) {
      $html->message('info', $lang{INFO}, "$lang{CHANGED}");
    }
  }
  elsif( $FORM{chg} ) {
    $Tags->info($FORM{chg});

    if (! _error_show($Tags)) {
      $html->message('info', $lang{INFO}, "$lang{CHANGING}");
      $Tags->{ACTION}     = 'change';
      $Tags->{LNG_ACTION} = $lang{CHANGE};
    }
  }
  elsif ($FORM{del} && $FORM{COMMENTS}) {
    $Tags->del($FORM{del});
    if (!$Tags->{errstr}) {
      $html->message('info', $lang{DELETED}, "$lang{DELETED} [$FORM{del}] ");
    }
  }

  $Tags->{PRIORITY_SEL} = $html->form_select(
    'PRIORITY',
    {
      SELECTED     => $Tags->{PRIORITY} || 2,
      SEL_ARRAY    => \@priority,
      STYLE        => \@priority_colors,
      ARRAY_NUM_ID => 1
    }
  );

  $html->tpl_show(_include('tags_form', 'Tags'), $Tags);

  result_former({
     INPUT_DATA      => $Tags,
     FUNCTION        => 'list',
     DEFAULT_FIELDS  => 'NAME,COMMENTS,PRIORITY',
     FUNCTION_FIELDS => 'change,del',
     EXT_TITLES      => {
        name         => $lang{NAME},
        priority     => $lang{PRIORITY},
        comments     => $lang{COMMENTS},
     },
     SKIP_USER_TITLE => 1,
     SELECT_VALUE    => {
        priority =>  {
          0  => "$lang{VERY_LOW}",
          1  => "$lang{LOW}",
          2  => "$lang{NORMAL}",
          3  => "$lang{HIGH}",
          4  => "$lang{VERY_HIGH}"
        }
     },
     TABLE           => {
       width      => '100%',
       caption    => "Tags",
       qs         => $pages_qs,
       ID         => 'IPTV_USERS_LIST',
     },
     MAKE_ROWS    => 1,
     MODULE       => 'Tags',
     TOTAL        => 1
    });

  return 1;
}


#**********************************************************
=head2 tags_search_form($attr)

=cut
#**********************************************************
sub tags_search_form {

  my $list = $Tags->list({ NAME      => '_SHOW',
                           PRIORITY  => '_SHOW',
                           COLS_NAME => 1 });
  my $form = '';

  foreach my $line (@$list) {
    my $input = $html->form_input('TAGS', $line->{id}, { ID => $line->{id}, TYPE => 'checkbox', STATE => ($FORM{TAGS} && in_array($line->{id}, [ split(/, /, $FORM{TAGS}) ] )) ? 'checked' : undef });

    $form .= $html->tpl_show(templates('form_row'), {
           ID      => "tag_$line->{id}",
           NAME    => $line->{name},
           VALUE   => $input,
           #BG_COLOR=> $priority_colors[$line->{priority}]
         },
         { OUTPUT2RETURN => 1 });
  }

  return $form;
}

#**********************************************************
=head2 tags_sel($attr)

=cut
#**********************************************************
sub tags_sel {

  my $form =  $html->form_select('TAGS',
              {
                SELECTED => $FORM{TAGS} || '',
                SEL_LIST => $Tags->list({
                              NAME      => '_SHOW',
                              PRIORITY  => '_SHOW',
                              COLS_NAME => 1 }),

                #SEL_OPTIONS => { '' => $lang{ALL} },
                EX_PARAMS   => 'multiple="multiple"'
              }
            );

  return $form;
}

#**********************************************************

=head2 analiz_user_tags () -

  Arguments:
    $attr -
  Returns:

  Examples:

=cut

#**********************************************************
sub reports_user_tags {
  my %user_tag_info;
  my %tag_info;
  my @priority_colors_tags = ('btn-muted', "btn-info", "btn-success", 'btn-warning', "btn-danger");
  my $tags = $Tags->tags_list({COLS_NAME => 1});

  foreach my $user_tag (@$tags) {
   $user_tag_info{ $user_tag->{id} } += defined($user_tag->{uid})?1:0;

    $tag_info{ $user_tag->{id} . 'name' }     = $user_tag->{name};
    $tag_info{ $user_tag->{id} . 'priority' } = $user_tag->{priority};
    $tag_info{all_usr_with_tags} += 1;
    if ($user_tag->{disable} && $user_tag->{disable} == 1) {
      $tag_info{ $user_tag->{id} . 'disable' } += 1;
    }
    elsif(defined($user_tag->{disable}) && $user_tag->{disable} == 0) {
      $tag_info{ $user_tag->{id} . 'active' } += 1;
    }
  }

  my $tags_table = $html->table(
    {
      caption    => "Tags",
      width      => '100%',
      title      => [ "$lang{NAME}", "$lang{ENABLE}", "$lang{DISABLE}", "$lang{TOTAL}", "$lang{PERCENTAGE}" ],
      cols_align => [ 'right', 'left', 'center', 'center', 'center' ],
      ID         => 'USER_TAGS_REPORTS'
    }
  );

  foreach my $tags_key (sort keys %user_tag_info) {
    $tags_table->addrow(
      $html->button($html->element('span', $tag_info{ $tags_key . 'name' }, { class => "btn btn-xs $priority_colors_tags[$tag_info{$tags_key . 'priority'}]" }), "index=7&search_form=1&TAGS=$tags_key&type=11&search=1", { class => 'button button-default' }),
      $tag_info{ $tags_key . 'active' }  ? $html->button($tag_info{ $tags_key . 'active' },  "index=7&search_form=1&TAGS=$tags_key&type=11&DISABLE=0&search=1", { class => 'button button-default' }) : 0,
      $tag_info{ $tags_key . 'disable' } ? $html->button($tag_info{ $tags_key . 'disable' }, "index=7&search_form=1&TAGS=$tags_key&type=11&DISABLE=1&search=1", { class => 'button button-default' }) : 0,
      $user_tag_info{$tags_key}          ? $html->button($user_tag_info{$tags_key},          "index=7&search_form=1&TAGS=$tags_key&type=11&search=1",           { class => 'button button-default' }) : 0,
      $html->progress_bar(
        {
          TOTAL        => $tag_info{all_usr_with_tags},
          COMPLETE     => $user_tag_info{$tags_key},
          PERCENT_TYPE => 1,
          COLOR        => 'MAX_COLOR',
        },
      )
    );
  }
  print $tags_table->show();

  return 1;
}

#**********************************************************
=head2 tags_multiuser_sel($attr) Multiuser form add tags

=cut
#**********************************************************
sub tags_multiuser_form {
  my $list  = $Tags->tags_user({ COLS_NAME => 1 });

  my $table = $html->table(
    {
      width      => '100%',
      title      => [ '-', "$lang{NAME}", $lang{DATE}, $lang{COMMENTS} ],
      FIELDS_IDS => $Tags->{COL_NAMES_ARR},
      cols_align => [ 'left', 'left', 'left', 'left' ],
      qs         => $pages_qs,
      ID         => 'TAG_USER',
    }
  );

  foreach my $line (@$list) {
    $table->addrow(
      #$line->{id}.
      $html->form_input(
        'TAGS_IDS',
        $line->{id},
        {
          TYPE  => 'CHECKBOX',
          FORM_ID => $FORM{FORM_ID},
        }
      ),
      $line->{name},
      $line->{date},
      $line->{comments}
    );
  }

  my $action = '';

  $action = $html->form_input('MULTIUSER', "$lang{CHANGE}", {
   TYPE    => 'submit',
   FORM_ID => $FORM{FORM_ID} });

  $table->addrow($action);

  print $table->show();

  return 1;
}

1

